*          DATA FILE 006 OF CBT MVS UTILITIES TAPE.  PGM=DOCFILE.
*             UPDATED 10/29/2017 BY SAM GOLOB.
*          DATA SET CBT1892    AT LEVEL 010 AS OF 03/27/99
*          DATA SET CBT1892    AT LEVEL 009 AS OF 07/20/89
*   CHANGE THE COMMENTS IN COLUMNS 72-80 FOR THE DOC FILE  AXC 07/89
*   TAKE THE POSSIBILITY OF LOWER CASE INTO ACCOUNT.       SBG 03/99
*   UPDATED FOR 4-DIGIT FILE NUMBERS.              .       SBG 10/17
CBT1892  SAVEALL REG=2             SAVE THE REGISTERS
         OPEN  (INPUT,(INPUT))     OPEN THE INPUT
         OPEN  (OUTPUT,(OUTPUT))   OPEN THE OUTPUT
READ     GET   INPUT,AREA          READ A RECORD
         MVC   OUT(72),AREA        MOVE 1ST BYTES TO OUTPUT
         MVC   OUTWK(10),AREA      MOVE 10 BYTES TO WKAREA SBG 03/99
         OC    OUTWK(10),=C'          '  UPPERCASE THEM    SBG 03/99
         MVC   OUTWK2(10),AREA+13        THIS AREA IS TELLTALE
         OC    OUTWK2(10),=C'          ' UPPERCASE IT
         CLC   =C'//***FILE',OUTWK   CHECK FOR FILE CHANGE SBG 10/17
         BE    MOVE                YES A FILE CHANGE
         CLC   =C'ADDITION S',OUTWK2 CHECK FOR BACK END    SBG 10/17
         BE    FILE000             YES BACK TO FILE 000
         CLC   =C'JCL SECTIO',OUTWK2 CHECK FOR BACK END    SBG 10/17
         BE    FILE000             YES BACK TO FILE 000
         B     WRITE               GO WRITE THE RECORD
MOVE     MVC   FILE(5),=C'FILE '   MOVE FILE CONSTANT      SBG 10/17
         MVC   NUMBER(4),AREA+9    MOVE FILE NUMBER        AXC 07/89
WRITE    MVC   OUTWK(10),=C'          '  BLANK WKAREA      SBG 03/99
         MVC   OUTWK2(10),=C'          ' BLANK WKAREA      SBG 10/17
         PUT   OUTPUT,OUTFILE      WRITE OUT THE NUMBER
         B     READ                GET A NEW RECORD
FILE000  MVC   FILE(8),=C'JCL FILE' SET TO JCL FILE        AXC 07/89
         B     WRITE               GO WRITE THE RECORD
EOJ      CLOSE OUTPUT              CLOSE THE INPUT
         CLOSE INPUT               CLOSE THE OUTPUT
         ENDALL
INPUT    DCB   DSORG=PS,DDNAME=INPUT,MACRF=GM,EODAD=EOJ
OUTPUT   DCB   DSORG=PS,DDNAME=OUTPUT,MACRF=PM,                        X
               LRECL=80,RECFM=FB
AREA     DS    CL80
OUTFILE  DS    0CL80
OUT      DS    CL72
FILE     DC    CL4'DOC '                                   SBG 10/17
NUMBER   DC    CL4'FILE'                                   SBG 10/17
OUTWK    DS    0CL10               DEFINE 10 BYTE WKAREA   SBG 03/99
OUTINIT  DC    CL10' '             INIT WKAREA TO BLANKS   SBG 03/99
OUTWK2   DS    0CL10               DEFINE 10 BYTE WKAREA   SBG 10/17
OUTINI2  DC    CL10' '             INIT WKAREA TO BLANKS   SBG 10/17
         END
