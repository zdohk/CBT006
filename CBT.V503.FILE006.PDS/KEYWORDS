./ ADD NAME=$$ARNIE  0100-20290-82165-1158-00006-00006-00000-FILE042 00
THIS MEMBER IS THE OLD CBT FILE 042 WHICH ARNIE HAD USED TO DO SOME
OF HIS HOUSEKEEPING.  SINCE I NEEDED THE FILE SLOT (FILE 042) FOR
ANOTHER FILE, AND THIS FILE HAS TO DO WITH CBT TAPE MAINTENANCE, I
DECIDED TO PUT THIS FILE HERE FOR SAFEKEEPING.  MAYBE SOMEBODY (OR I)
MIGHT FIND IT USEFUL.  I DON'T EVER WANT TO THROW GOOD PROGRAMMMING
AWAY.  (SG-OCT 2020)
./ ADD NAME=$DOC     0100-20290-82165-1856-00005-00005-00000-FILE042 00
$DOC ....... THIS FILE
JCL ........ THE JCL I USE AT CBT TO RUN
KEYWORDS ... A FILE CONTAINING WORDS THAT SHOULD NOT BE KEYWORDED
PROGRAM .... THE ACTUAL CODE TO FORMAT FILE 001 INTO THE PROPER
             FORMAT
./ ADD NAME=JCL      0100-82122-82165-1856-00092-00092-00000-FILE042 00
//TS155007 JOB 527TSO000S0008,ARNIE,CLASS=J,MSGCLASS=5,NOTIFY=TS15500,
// REGION=1024K
//*
//* THIS IS THE JCL I USE TO LOAD AT CBT
//* IF YOU HAVE PROBLEMS CALL ME - ARNOLD  (203)-244-5495
//*
//STEP00 EXEC PGM=CBT299              /* PGM TO DELETE DATASETS */
TEC.CBT.INFO
TEC.CBT.OZB
//STEP01 EXEC PAN,SO=5               /* GET FILE ONE OUT OF PAN */
++WRITE WORK,MVSMODS1                /* OR FROM YOUR FILE 001   */
//*
//*  CONVERT IT TO BE PROCESSED BY INFO
//*
//STEP02    EXEC PGM=CBT1342,REGION=4096K            /* HNB PGM      */
//SYSIN     DD   DSN=TS15500.CBT1342K.DATA,DISP=SHR  /* KEYWORDS     */
//FILEIN    DD   DSN=&&WORK,DISP=(OLD,DELETE,DELETE) /* FILE ONE     */
//FILEOUT   DD   DSN=TEC.CBT.INFO,DISP=(,CATLG),     /* OUT FILE FOR */
//          UNIT=SYSDA,SPACE=(CYL,(20,5)),           /* NEXT STEP    */
//          DCB=(RECFM=VB,LRECL=255,BLKSIZE=3120,DSORG=PS)
//SYSUDUMP  DD   SYSOUT=5                            /* DUMP         */
//*
//STEP03    EXEC PGM=IEBGENER                        /* MERGE HEADER */
//SYSIN     DD   DUMMY                               /* DUMMY        */
//SYSPRINT  DD   SYSOUT=5                            /* SYSPRINT     */
//SYSUT2    DD   DSN=TEC.CBT.INFO,DISP=MOD           /* OUT OF PGM 1 */
//SYSUT1    DD   DSN=TEC.ARNIE.INFO.G3,DISP=SHR      /* FILE 5 OF TAPE *
//*
//*  LET INFO DO SOME STUFF TO IT
//*
//STEP04    EXEC PGM=BLGOZB                          /* LET OZ DO IT */
//FILE      DD   DSN=TEC.CBT.INFO,DISP=SHR           /* INPUT TO STEP*/
//SORTMSG   DD   SYSOUT=5                            /* MESSAGES     */
//SYSPRINT  DD   SYSOUT=5                            /* SYSPRINT     */
//SYSOUT    DD   SYSOUT=5                            /* SYSOUT       */
//SORTLIB   DD   DSN=SYS1.SORTLIB,DISP=SHR           /* SORTLIB      */
//SORTIN    DD   UNIT=SYSDA,SPACE=(CYL,(10,10))      /* SORTIN       */
//SORTOUT   DD   UNIT=SYSDA,SPACE=(CYL,(10,10))      /* SORTOUT      */
//SORTWK01  DD   UNIT=SYSDA,SPACE=(CYL,(10,10))      /* SORTWORK     */
//SORTWK02  DD   UNIT=SYSDA,SPACE=(CYL,(10,10))      /* SORTWORK     */
//SORTWK03  DD   UNIT=SYSDA,SPACE=(CYL,(10,10))      /* SORTWORK     */
//TAPE      DD   DSN=TEC.CBT.OZB,DISP=(,CATLG),      /* OUT FROM STEP*/
//          UNIT=SYSDA,SPACE=(CYL,(20,5)),
//          DCB=(RECFM=VB,LRECL=255,BLKSIZE=3120,DSORG=PS)
//*
//STEP05 EXEC VSAMLIST,SO=5,CAT=VSM02CAT                   /* LIST VSAM
//*
//*  LOAD IT TO DATABASE
//*
//STEP06    EXEC PGM=BLGOZC                          /* RELOAD D.B.  */
//TAPE      DD   DSN=TEC.CBT.OZB,DISP=SHR            /* INPUT FILE   */
//OZVSAM    DD   DSN=VSAM02.CBTMODS.OZ,DISP=OLD      /* OUTPUT FILE  */
//SYSPRINT  DD   SYSOUT=5                            /* SYSPRINT     */
//SYSIN     DD   *                                   /* SELECT CARD  */
$SELECT *
//*
//STEP07 EXEC PGM=CBT299                             /* DELETE PGM  */
TEC.CBT.INFO
TEC.CBT.OZB
/*
//
//
//
//
//TXXXXXXX JOB 527TSO000S0008,ARNIE,CLASS==,MSGCLASS=A,NOTIFY=TS15500,
// REGION=1024K
//*
//* THIS IS A DUMMY JOB FOR ME TO ADD SOME ADDITIONAL STEPS HERE
//*
//*  ALLOCATE THE DATABASE - AT LEAST FOR THE FIRST TIME ANYWAYS
//*
//IDCAMS#4  EXEC PGM=IDCAMS,REGION=1024K
//SYSPRINT  DD   SYSOUT=1
//SYSIN     DD   *
 DELETE VSAM02.CBTMODS.OZ CLUSTER
 DEFINE CLUSTER(NAME('VSAM02.CBTMODS.OZ') VOL(DAV002) IXD -
                KEYS(14 0) CYL(5 1) RECSZ(600 814) -
                CISZ(4096) SHR(3 3) REUSE SPEED ) -
        DATA   (NAME('VSAM02.CBTMODS.OZ.DATA')) -
        INDEX  (NAME('VSAM02.CBTMODS.OZ.INDEX'))
/*
//*
//*
//* REPRO THE HELP OVER  FROM IBMS INFO
//* CAN NOT SEND THIS SINCE IT IS COPYRIGHTED
//*
//IDCAMS#6  EXEC PGM=IDCAMS,REGION=1024K
//SYSPRINT  DD   SYSOUT=1
//SYSIN     DD   *
 REPRO IDS(VSAM02.BLGOZS.VSAM) ODS(VSAM02.CBTMODS.OZ) -
 FROMKEY(HH0000000000) TOKEY(HH9999999999)
//*
./ ADD NAME=KEYWORDS 0100-82122-82165-1856-00012-00012-00000-FILE042 00
A
AND
THE
THAT
IS
ARE
ALL
WILL
GIVE
YOU
ON
ENDTABLE
./ ADD NAME=PROGRAM  0100-82122-82165-1856-00513-00513-00000-FILE042 00
*          DATA SET CBT1342    AT LEVEL 003 AS OF 06/14/82
*          DATA SET CBT1342    AT LEVEL 002 AS OF 06/09/82              00001
*          DATA SET DED121     AT LEVEL 006 AS OF 06/09/82              00002
*                                                                       00003
*  MODIFIED 06/14/82 BY HNB VIA CBT TO INCREASE THE GETMAIN FOR THE     00004
*  KEYWORD TABLE (FROM 4096 TO 16364)                                   00005
*                                                                       00006
         MACRO                                                          00007
&NAME    SAVEALL &RENT,&REG=3,&EQU=YES,&ID=NO                           00008
         GBLB  &SAVRENT                                                 00009
         GBLB  &REGSW                                                   00010
         GBLB  &TIME                                                    00011
         GBLC  &SAVNDX                                                  00012
&SAVNDX  SETC  '&SYSNDX'                                                00013
&NAME    CSECT                                                          00014
         AIF   ('&SAVRENT' EQ '1').NOTFREE                              00015
         AIF   ('&REGSW' EQ '1').NOEQU                                  00016
         AIF   ('&EQU' NE 'YES').NOEQU                                  00017
&REGSW   SETB  1                                                        00018
         SPACE                                                          00019
*********************************************************************** 00020
********           DEFINITION OF GENERAL REGISTERS             ******** 00021
*********************************************************************** 00022
R0       EQU   0    GENERAL REGISTER ZERO                               00023
R1       EQU   1    GENERAL REGISTER ONE                                00024
R2       EQU   2    GENERAL REGISTER TWO                                00025
R3       EQU   3    GENERAL REGISTER THREE                              00026
R4       EQU   4    GENERAL REGISTER FOUR                               00027
R5       EQU   5    GENERAL REGISTER FIVE                               00028
R6       EQU   6    GENERAL REGISTER SIX                                00029
R7       EQU   7    GENERAL REGISTER SEVEN                              00030
R8       EQU   8    GENERAL REGISTER EIGHT                              00031
R9       EQU   9    GENERAL REGISTER NINE                               00032
R10      EQU   10   GENERAL REGISTER TEN                                00033
R11      EQU   11   GENERAL REGISTER ELEVEN                             00034
R12      EQU   12   GENERAL REGISTER TWELVE                             00035
R13      EQU   13   GENERAL REGISTER THIRTEEN                           00036
R14      EQU   14   GENERAL REGISTER FOURTEEN                           00037
R15      EQU   15   GENERAL REGISTER FIFTEEN                            00038
         SPACE                                                          00039
RA       EQU   10   GENERAL REGISTER TEN                                00040
RB       EQU   11   GENERAL REGISTER ELEVEN                             00041
RC       EQU   12   GENERAL REGISTER TWELVE                             00042
RD       EQU   13   GENERAL REGISTER THIRTEEN                           00043
RE       EQU   14   GENERAL REGISTER FOURTEEN                           00044
RF       EQU   15   GENERAL REGISTER FIFTEEN                            00045
*********************************************************************** 00046
********             END OF REGISTER DEFINITIONS               ******** 00047
*********************************************************************** 00048
         SPACE                                                          00049
.NOEQU   ANOP                                                           00050
         SAVE  (14,12)                                                  00051
         AIF   ('&REG' EQ '').MESS                                      00052
         BALR  &REG(1),0                                                00053
         AIF   (N'&REG EQ 1).A                                          00054
         AIF   (N'&REG EQ 2).B                                          00055
         AIF   (N'&REG EQ 3).C                                          00056
         AIF   (N'&REG EQ 4).D                                          00057
         AIF   (N'&REG EQ 5).E                                          00058
         MNOTE *****,'EXCESSIVE BASE REGISTERS -- 5 ASSUMED *****'      00059
         AGO   .E                                                       00060
.MESS    MNOTE 12,'***** NO BASE REGISTER SPECIFIED *****'              00061
         MEXIT                                                          00062
.NOTFREE MNOTE 12,'***** ENDALL NOT ISSUED PRIOR TO NEW SAVEALL *****'  00063
         MEXIT                                                          00064
.E       USING *,&REG(1),&REG(2),&REG(3),&REG(4),&REG(5)                00065
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS        00066
         L     &REG(3),=A(Q&SYSNDX+8192)                                00067
         L     &REG(4),=A(Q&SYSNDX+12288)                               00068
         L     &REG(5),=A(Q&SYSNDX+16384)                               00069
         AGO   .END                                                     00070
.D       USING *,&REG(1),&REG(2),&REG(3),&REG(4)                        00071
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS        00072
         L     &REG(3),=A(Q&SYSNDX+8192)                                00073
         L     &REG(4),=A(Q&SYSNDX+12288)                               00074
         AGO   .END                                                     00075
.C       USING *,&REG(1),&REG(2),&REG(3)                                00076
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS        00077
         L     &REG(3),=A(Q&SYSNDX+8192)                                00078
         AGO   .END                                                     00079
.B       USING *,&REG(1),&REG(2)                                        00080
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTER         00081
         AGO   .END                                                     00082
.A       USING *,&REG(1)                                                00083
.END     ANOP                                                           00084
         AIF   ('&RENT' NE 'RENT').NORENT                               00085
&SAVRENT SETB  1                                                        00086
         GETMAIN R,LV=4096         GET STORAGE FOR SAVE AREA            00087
         ST    13,4(1)             SET UP FORWARD CHAIN                 00088
         ST    1,8(13)             SET UP BACKWARD CHAIN                00089
         L     0,20(0,13)          RESET REGISTER 0                     00090
         L     1,24(0,13)          RESET REGISTER 1                     00091
         L     13,8(0,13)          LOAD SAVE AREA ADDRESS               00092
         B     J&SYSNDX           BRANCH AROUND LTORG                   00093
         AGO   .RENT                                                    00094
.NORENT  ANOP                                                           00095
&TIME    SETB  1                                                        00096
         ST    13,SAVE&SYSNDX+4    SET UP FORWARD CHAIN                 00097
         LA    1,SAVE&SYSNDX                                            00098
         ST    1,8(0,13)           SET UP BACKWARD CHAIN                00099
         L     1,24(0,13)          RESET REGISTER 1                     00100
         LA    13,SAVE&SYSNDX                                           00101
         B     J&SYSNDX           BRANCH AROUND BASE REGISTER CONST     00102
SAVE&SYSNDX DC 18F'0'                                                   00103
.RENT    ANOP                                                           00104
         LTORG                                                          00105
         AIF   ('&ID' NE 'YES').NOID                                    00106
         DC    CL8'&SYSDATE'                                            00107
         DC    CL8'&SYSTIME'                                            00108
.NOID    ANOP                                                           00109
J&SYSNDX DS    0H                                                       00110
         MEND                                                           00111
         MACRO                                                          00112
&NAME    ENDALL &CODE=                                                  00113
         GBLB  &TIME                                                    00114
         GBLB  &SAVRENT                                                 00115
         GBLC  &SAVNDX                                                  00116
         LCLA  &LEN                                                     00117
         LCLC  &REG                                                     00118
         AIF   ('&CODE' GT '4095').INVRET                               00119
&NAME    DS    0H                                                       00120
         AIF   (K'&CODE LE 0).CHKRENT                                   00121
         AIF   ('&CODE'(1,1) NE '(').CHKRENT                            00122
         AIF   ('&CODE' EQ '(13)' OR '&CODE' EQ '(14)').INVREG          00123
         AIF   ('&CODE' EQ '(0)' OR '&CODE' EQ '(1)').INVREG            00124
         AIF   ('&CODE' NE '(15)').CHKRENT                              00125
         LR    2,15               SAVE REG 15 RETURN CODE               00126
.CHKRENT AIF   (NOT &SAVRENT).NOTRENT                                   00127
&SAVRENT SETB  0                                                        00128
         LR    1,13               LOAD SAVE AREA ADDRESS                00129
         L     13,4(0,13)         RE-LOAD REGISTER 13                   00130
         FREEMAIN R,LV=4096,A=(1) FREE SAVE AREA STORAGE                00131
         AGO   .RENT                                                    00132
.NOTRENT ANOP                                                           00133
         AIF   (NOT &TIME).NOSAVE                                       00134
&TIME    SETB  0                                                        00135
         L     13,SAVE&SAVNDX+4   RE-LOAD REGISTER 13                   00136
.RENT    ANOP                                                           00137
         AIF   ('&CODE' EQ '').COND1                                    00138
         AIF   ('&CODE' NE '(15)').CHKREG                               00139
         LR    15,2               GET RETURN CODE BACK FROM R2          00140
         AGO   .ENDIT                                                   00141
.CHKREG  AIF   ('&CODE'(1,1) NE '(').NOTREG                             00142
&LEN     SETA  K'&CODE-2                                                00143
&REG     SETC  '&CODE'(2,&LEN)                                          00144
         LR    15,&REG            GET RETURN CODE                       00145
         AGO   .ENDIT                                                   00146
.NOTREG  ANOP                                                           00147
         LA    15,&CODE           LOAD CODE TO REG 15                   00148
         AGO   .ENDIT                                                   00149
.INVRET  MNOTE 12,'***** RETURN CODE PASSED GREATER THAN 4095 *****'    00150
         MEXIT                                                          00151
.NOSAVE  MNOTE 12,'***** SAVEALL NOT ISSUED PRIOR TO ENDALL *****'      00152
         MEXIT                                                          00153
.INVREG  MNOTE 12,'***** INVALID REGISTER USED FOR RETURN CODE "&CODE"' 00154
         MEXIT                                                          00155
.COND1   LA    15,0               LOAD ZERO (NO CODE) TO REG 15         00156
.ENDIT   ANOP                                                           00157
         RETURN     (14,12),T,RC=(15)                                   00158
         MEND                                                           00159
CBTINFO  SAVEALL REG=(3,12)                                             00160
         EJECT                                                          00161
         OPEN  (SYSIN,(INPUT),FILEIN,(INPUT),FILEOUT,(OUTPUT))          00162
         SPACE                                                          00163
         GETMAIN R,LV=16364             GET STOR FOR NON-KEYW TBL CBT   00164
*        GETMAIN R,LV=4096              GET STOR FOR NON-KEYW TBL CBT   00165
         SPACE                                                          00166
         LR    R4,R1                    POINT AT IT                     00167
         USING NOKWTBL,R4               ADDR TO IT                      00168
         ST    R4,SAVENOKW              SAVE PTR TO BEGINNING OF IT     00169
         SPACE                                                          00170
         GETMAIN R,LV=98304             GET STOR FOR KEYW USED TBL      00171
         SPACE                                                          00172
         LR    R6,R1                    POINT AT IT                     00173
         ST    R6,SAVETABL              AND SAVE ADDRESS                00174
         ST    R6,NEXTENTR              AND SAVE ADDRESS                00175
         USING KWTBL,R6                 ADDR TO IT                      00176
         EJECT                                                          00177
GETLP    GET   SYSIN,NOKW               GET A KEYWORD                   00178
         CLC   0(8,R4),ENDTABLE         END OF TABLE                    00179
         BE    ENDREAD                      YES...END OF KEYWORD DS     00180
         LA    R4,30(R4)                    NO MAX KEYWORD LENGTH=30    00181
         B     GETLP                        AND LOOP                    00182
         SPACE                                                          00183
ENDREAD  SR    R10,R10                  INIT REG                        00184
         AH    R10,ONE                  SET THIS IS FIRST TOPIC         00185
         SPACE                                                          00186
STARTUP  MVC   INREC(255),BLANK255      CLEAN OUT ENTRY FIRST           00187
         GET   FILEIN,INREC             GET A RECORD                    00188
         TR    INREC(255),TRTABLE       SET TO UPPER CASE               00189
         CLC   INREC(12),SECTION        START OF A TOPIC?               00190
         BNE   STARTUP                     NO..CONTINUE                 00191
         SPACE                                                          00192
FIRSTONE MVC   TOPIC(58),INREC+5        SAVE TOPIC NAME                 00193
         LA    R2,TABLE                 START OF TABLE                  00194
         MVC   0(253,R2),INREC+2        MOVE TO TABLE                   00195
         MVC   70(185,R2),BLANK255      MOVE BLANKS TO REST OF RECORD   00196
         SPACE                                                          00197
LOOP1    MVC   INREC(255),BLANK255      CLEAN OUT ENTRY FIRST           00198
         GET   FILEIN,INREC             GET A RECORD                    00199
         TR    INREC(255),TRTABLE       SET TO UPPER CASE               00200
         CLC   INREC(12),SECTION        START OF A TOPIC?               00201
         BE    ISNEW                       YES                          00202
         CLC   INREC(9),NEWFILE         START OF A TOPIC?               00203
         BE    ISNEW                       YES                          00204
         LA    R2,255(R2)               BUMP TABLE POINTER              00205
         MVC   0(253,R2),INREC+2        MOVE TO TABLE                   00206
         MVC   70(185,R2),BLANK255      MOVE BLANKS TO REST OF RECORD   00207
         B     LOOP1                                                    00208
         SPACE                                                          00209
ISNEW    LA    R2,255(R2)               BUMP POINTER                    00210
         MVC   0(255,R2),BLANK255       CLEAN OUT ENTRY FIRST           00211
         MVC   0(72,R2),$EOMLINE        MOVE $EOM TO END OF ENTRY       00212
         CVD   R10,DWORK                CONVERT COUNTER TO DECIMAL      00213
         UNPK  TOPIC#,DWORK             UNPACK IT                       00214
         OI    TOPIC#+5,X'F0'           FIX IT UP                       00215
         MVC   TTOPIC(58),TOPIC         MOVE TOPIC TO TITLE LINE        00216
         MVC   KTOPIC(58),TOPIC         MOVE TOPIC TO KEYWORD LINE      00217
         PUT   FILEOUT,HEADER           MOVE HEADER TO OUTPUT           00218
         PUT   FILEOUT,TITLE            MOVE TITLE  TO OUTPUT           00219
         PUT   FILEOUT,KEYWORD          MOVE KEYWORD TO OUTPUT          00220
         LA    R2,TABLE                 POINT TO BEGINNING OF TABLE     00221
         SR    R11,R11                  INIT REG                        00222
         LH    R11,=H'4'                INITIALIZE AS 4TH LINE          00223
         SPACE                                                          00224
         B     DOKEYW                   GO DO KEYWORDING OF 'L' LINES   00225
         EJECT                                                          00226
ENDKW    MVC   OUTREC(72),BLANKL        MOVE BLANKLINE TO OUTPUT        00227
         MVC   OUTREC+4(8),BLANK255     INIT SEQ # WITH BLANKS          00228
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL        00229
         UNPK  OUTREC+4(8),DWORK        UNPACK IT INTO LINE             00230
         OI    OUTREC+11,X'F0'          FIX IT UP                       00231
         PUT   FILEOUT,OUTREC           MOVE LINE TO OUTPUT             00232
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER           00233
         LA    R2,TABLE                 POINT TO BEGINNING OF ENTRY TBL 00234
         SPACE                                                          00235
LOOP2    CLC   0(4,R2),=C'$EOM'         IS IT END OF ENTRY?             00236
         BE    ENDENTRY                      YES                        00237
         MVC   OUTREC+12(243),0(R2)     MOVE A LINE TO OUTPUT REC,      00238
*                                       MAKE ROOM FOR SEQ # + RDW       00239
         MVC   OUTREC(4),RDW            MOVE IN DEFAULT RDW             00240
         MVC   OUTREC+4(8),BLANK255     INIT SEQ # WITH BLANKS          00241
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL        00242
         UNPK  OUTREC+4(8),DWORK        UNPACK IT INTO LINE             00243
         OI    OUTREC+11,X'F0'          FIX IT UP                       00244
         PUT   FILEOUT,OUTREC           MOVE LINE TO OUTPUT             00245
         LA    R2,255(R2)               BUMP POINTER                    00246
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER           00247
         B     LOOP2                         AND LOOP                   00248
         SPACE                                                          00249
ENDENTRY MVC   OUTREC+12(243),0(R2)     MOVE A $EOM LINE TO OUTPUT AND  00250
*                                       MAKE ROOM FOR SEQ # + RDW       00251
         MVC   OUTREC(4),RDW            MOVE IN DEFAULT RDW             00252
         MVC   OUTREC+4(8),BLANK255     INIT SEQ # WITH BLANKS          00253
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL        00254
         UNPK  OUTREC+4(8),DWORK        UNPACK IT INTO LINE             00255
         OI    OUTREC+11,X'F0'          FIX IT UP                       00256
         PUT   FILEOUT,OUTREC           MOVE LINE TO OUTPUT             00257
         CLI   ENDSW,C'1'               WAS THIS THE LAST ENTRY?        00258
         BE    ENDIT                        YES DO FINAL PROCESSING     00259
         LA    R2,TABLE                 POINT TO BEGINNING OF TABLE     00260
         AH    R10,ONE                  SET NEXT TOPIC NUMBER           00261
         MVC   0(253,R2),INREC+2        MOVE TO TABLE                   00262
         MVC   70(185,R2),BLANK255      MOVE BLANKS TO REST OF RECORD   00263
         MVC   TOPIC(58),INREC+5        SAVE TOPIC NAME                 00264
         LA    R2,255(R2)               BUMP TABLE POINTER              00265
         B     LOOP1                    AND CONTINUE                    00266
         EJECT                                                          00267
DOKEYW   LA    R2,TABLE                 POINT TO BEGINNING OF TABLE     00268
         MVC   NEXTENTR,SAVETABL        RESET NEXT ENTRY POINTER        00269
         SPACE                                                          00270
MAINLP   BAL   R14,FINDWORD             GO GET A WORD                   00271
         CLC   0(5,R7),=C'$EOM '        END OF ENTRY?                   00272
         BE    BLDL                         YES... BUILD 'L' KW LINES   00273
         BAL   R14,TESTTBL              IS IN TBL OF BAD WORDS?         00274
         LTR   R15,R15                  TEST RETURN CODE                00275
         BNZ   MAINLP                       IN TABLE, GET NEXT WORD     00276
         BAL   R14,CHKWORD              ADD TO TABLE IF NOT USED        00277
         B     MAINLP                   LOOP TILL END OF DOCUMENT       00278
         EJECT                                                          00279
EOFILE   MVI   ENDSW,C'1'               THIS IS THE END                 00280
         B     ISNEW                    DO IT ONE MORE TIME             00281
         SPACE                                                          00282
ENDIT    L     R4,SAVENOKW              ADDRESS OF NOKEYWORD TABLE      00283
         SPACE                                                          00284
         FREEMAIN R,LV=16364,A=(R4)      FREE STORAGE     CBT           00285
*        FREEMAIN R,LV=4096,A=(R4)       FREE STORAGE     CBT           00286
         SPACE 2                                                        00287
         L     R6,SAVETABL              ADDRESS OF KEYWD USED TABLE     00288
         SPACE                                                          00289
         FREEMAIN R,LV=98304,A=(R6)     FREE STORAGE                    00290
         EJECT                                                          00291
         CLOSE (FILEIN,,FILEOUT,,SYSIN)                                 00292
         SPACE                                                          00293
         ENDALL                                                         00294
         EJECT                                                          00295
FINDWORD CLI   0(R2),C' '               IS IT A BLANK?                  00296
         BNE   FINDEND                       NO.... WE HAVE A SOMETHING 00297
FINDBMP1 LA    R2,1(R2)                      BUMP                       00298
         B     FINDWORD                      AND LOOP                   00299
         SPACE                                                          00300
FINDEND  LR    R7,R2                    SAVE PTR TO BEGINNING OF WORD   00301
         SR    R8,R8                    GET READY TO GET LENGTH OF WORD 00302
         AH    R8,ONE                   SET LENGTH TO ONE               00303
FINDBL   CLI   0(R2),C' '               BLANK?                          00304
         BER   R14                           YES.. HAVE END OF WORD     00305
         LA    R2,1(R2)                      NO... BUMP                 00306
         AH    R8,ONE                        ADD 1 TO LENGTH            00307
         B     FINDBL                        AND LOOP                   00308
         EJECT                                                          00309
TESTTBL  L     R4,SAVENOKW              PT TO BEGIN OF BAD KEYWORDS     00310
         BCTR  R8,0                     DECREMENT FOR EX INSTR          00311
         SR    R15,R15                  RC = 0                          00312
TESTLP   CLC   0(8,R4),ENDTABLE         IS THIS END OF TABLE?           00313
         BER   R14                           YES.. CONTINUE             00314
         EX    R8,CLC                   IN TBL OF WORDS NOT TO BE USED? 00315
         BE    BADWORD                       YES.. DON'T USE IT         00316
         LA    R4,30(R4)                     NO... BUMP                 00317
         B     TESTLP                              AND LOOP             00318
         SPACE                                                          00319
BADWORD  LA    R15,4                    RC = 4..WORD IS N.G.            00320
         BR    R14                      AND RETURN                      00321
         EJECT                                                          00322
CHKWORD  L     R6,SAVETABL              PT TO BEGIN OF USED KEYWORDS    00323
CHKWDLP  C     R6,NEXTENTR              END OF USED ENTRIES?            00324
         BNL   TBLADD                        YES.. ADD WORD TO TBL      00325
         EX    R8,CLCUSED               WAS WORD USED YET?              00326
         BER   R14                           YES.. DONT ADD             00327
         LA    R6,30(R6)                BUMP                            00328
         B     CHKWDLP                  AND LOOP                        00329
         SPACE                                                          00330
TBLADD   MVC   0(30,R6),BLANK255        BLANK OUT SPOT IN TABLE FIRST   00331
         EX    R8,MVC                   NEW WORD ADD TO TBL             00332
         LA    R6,30(R6)                BUMP TABLE POINTER              00333
         ST    R6,NEXTENTR              AND SAVE FOR LATER              00334
         BR    R14                      RETURN TO MAINLINE              00335
         EJECT                                                          00336
BLDL     L     R6,SAVETABL              PT TO BEGINNING OF USED WORDS   00337
         SR    R8,R8                    CLEAR A REG                     00338
BLDLLP   C     R6,NEXTENTR              END OF TABLE?                   00339
         BE    CHKLASTL                      YES... SEE IF ONE MORE KW  00340
         CH    R8,TWO                   TWO WORDS THIS LINE?            00341
         BE    PUTLCARD                      YES... DONE THIS LINE      00342
         CH    R8,ONE                   ONE WORD THIS LINE?             00343
         BE    DOLKW2                        YES... DONE THIS LINE      00344
LMOVE1   MVC   LKW1(28),0(R6)           MOVE KEYWORD TO LINE            00345
LBUMP    AH    R8,ONE                   ADD ONE TO COUNTER              00346
         LA    R6,30(R6)                     BUMP                       00347
         B     BLDLLP                        AND LOOP                   00348
DOLKW2   MVC   LKW2(28),0(R6)           MOVE IN 2ND KEYWORD THIS LINE   00349
         B     LBUMP                         AND BUMP                   00350
         SPACE                                                          00351
PUTLCARD MVC   LSEQ(8),BLANK255         INIT SEQ # WITH BLANKS          00352
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL        00353
         UNPK  LSEQ(8),DWORK            UNPACK IT INTO LINE             00354
         OI    LSEQ+7,X'F0'             FIX IT UP                       00355
         MVC   OUTREC(255),BLANK255     CLEAR OUT OUTPUT WORKAREA       00356
         MVC   OUTREC(72),LLINE         MOVE LLINE TO OUTPUT            00357
         PUT   FILEOUT,OUTREC           WRITE IT OUT                    00358
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER           00359
         MVC   LKW1(28),BLANK255        CLEAR OUT 1ST KEYWORD           00360
         MVC   LKW2(28),BLANK255        CLEAR OUT 2ND KEYWORD           00361
         SR    R8,R8                    RESET COUNTER                   00362
         B     LMOVE1                   AND CONTINUE                    00363
         SPACE                                                          00364
CHKLASTL CLC   LKW1(28),BLANK255        IS THE KEYWORD BLANK?           00365
         BE    ENDKW                          YES... DONE KWD'ING       00366
         MVC   LSEQ(8),BLANK255         INIT SEQ # WITH BLANKS          00367
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL        00368
         UNPK  LSEQ(8),DWORK            UNPACK IT INTO LINE             00369
         OI    LSEQ+7,X'F0'             FIX IT UP                       00370
         MVC   OUTREC(255),BLANK255     CLEAR OUT OUTPUT WORKAREA       00371
         MVC   OUTREC(72),LLINE         MOVE LLINE TO OUTPUT            00372
         PUT   FILEOUT,OUTREC           WRITE IT OUT                    00373
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER           00374
         B     ENDKW                    NOW DONE KWD'ING                00375
         EJECT                                                          00376
*                                                                       00377
*   HEADER RECORD FORMAT                                                00378
*                                                                       00379
HEADER   DS    0CL72                                                    00380
HRDW     DC    X'00480000'                                              00381
HSEQ     DC    C'00000001'                                              00382
H        DC    C'H'                                                     00383
         DC    C' '                                                     00384
FILENAME DC    C'E'                                                     00385
TOPIC#   DS    CL6                                                      00386
         DC    C' S=MVS C=G'                                            00387
         DC    CL59' '                                                  00388
*                                                                       00389
*   TITLE LINE  FORMAT                                                  00390
*                                                                       00391
TITLE    DS    0CL72                                                    00392
TRDW     DC    X'00480000'                                              00393
TSEQ     DC    C'00000002'                                              00394
T        DC    C'T'                                                     00395
         DC    C' '                                                     00396
TTOPIC   DS    CL58                                                     00397
         DC    CL20' '                                                  00398
         SPACE                                                          00399
*                                                                       00400
*   KEYWORD LINE FORMAT                                                 00401
*                                                                       00402
KEYWORD  DS    0CL72                                                    00403
KRDW     DC    X'00480000'                                              00404
KSEQ     DC    C'00000003'                                              00405
K        DC    C'K'                                                     00406
         DC    C' '                                                     00407
KTOPIC   DS    CL58                                                     00408
         DC    CL20' '                                                  00409
         SPACE                                                          00410
*                                                                       00411
* L KEYWORD LINE FORMAT                                                 00412
*                                                                       00413
LLINE    DS    0CL72                                                    00414
LRDW     DC    X'00480000'                                              00415
LSEQ     DS    CL8                                                      00416
L        DC    C'L'                                                     00417
         DC    C' '                                                     00418
LKW1     DS    CL28                                                     00419
         DC    C' '                                                     00420
LKW2     DS    CL28                                                     00421
         DC    C' '                                                     00422
         SPACE                                                          00423
*                                                                       00424
*   BLANK LINE FORMAT                                                   00425
*                                                                       00426
BLANKL   DS    0CL72                                                    00427
BRDW     DC    X'00480000'                                              00428
BSEQ     DS    CL8                                                      00429
         DC    CL70' '                                                  00430
*                                                                       00431
*   $EOM LINE FORMAT                                                    00432
*                                                                       00433
$EOMLINE DS    0CL72                                                    00434
         DC    CL4'$EOM'                                                00435
         DC    CL68' '                                                  00436
         SPACE                                                          00437
         EJECT                                                          00438
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,EODAD=ENDREAD,                    X00439
               LRECL=80,                                               X00440
               MACRF=(GM),RECFM=FB                                      00441
         EJECT                                                          00442
         SPACE                                                          00443
FILEIN   DCB   DDNAME=FILEIN,DSORG=PS,EODAD=EOFILE,                    X00444
               MACRF=(GM)                                               00445
         EJECT                                                          00446
FILEOUT  DCB   BLKSIZE=3120,DDNAME=FILEOUT,DSORG=PS,                   X00447
               LRECL=255,                                              X00448
               MACRF=(PM),RECFM=VB                                      00449
         EJECT                                                          00450
         LTORG                                                          00451
         EJECT                                                          00452
*                                                                       00453
* INPUT OUTPUT WORK AREA                                                00454
*                                                                       00455
         DS   0D                        FORCE DOUBLE WORD ALIGNMENT     00456
DWORK    DC    D'0'                                                     00457
SAVENOKW DC    F'0'                                                     00458
SAVETABL DC    F'0'                                                     00459
SAVEENTR DC    F'0'                                                     00460
NEXTENTR DC    F'0'                                                     00461
ONE      DC    H'1'                                                     00462
TWO      DC    H'2'                                                     00463
RDW      DC    X'00540000'              DEFAULT RDW 84 BYTE RECORD      00464
ENDTABLE DC    C'ENDTABLE'                                              00465
NEWFILE  DC    C'//***FILE'                                             00466
SECTION  DC    C'//***SECTION'                                          00467
BLANK255 DC    255C' '                                                  00468
         SPACE                                                          00469
CLC      CLC   0(1,R4),0(R7)            IS THIS WORD NOT TO BE USED?    00470
CLCUSED  CLC   0(1,R6),0(R7)            WAS WORD USED YET?              00471
MVC      MVC   0(1,R6),0(R7)            ADD WORD TO USED TABLE          00472
         SPACE                                                          00473
ENDSW    DS    CL1                                                      00474
SAVEPARM DS    CL3                                                      00475
UNPKWORK DS    CL6                                                      00476
TOPIC    DS    CL58                                                     00477
INREC    DS    CL255                                                    00478
OUTREC   DS    CL255                                                    00479
         SPACE                                                          00480
TRTABLE  DC    CL255' '                                                 00481
         ORG   TRTABLE+X'40'                                            00482
         DC    C'          ¢.<(+|&&         !$*);¬-/        ¦,%_>?'     00483
         ORG   TRTABLE+X'70'                                            00484
         DC    C'         `:#@''=" '                                    00485
         ORG   TRTABLE+X'81'                                            00486
         DC    C'ABCDEFGHI       '                                      00487
         ORG   TRTABLE+X'91'                                            00488
         DC    C'JKLMNOPQR       ~'                                     00489
         ORG   TRTABLE+X'A2'                                            00490
         DC    C'STUVWXYZ                      {'                       00491
         ORG   TRTABLE+C'A'                                             00492
         DC    C'ABCDEFGHI      }'                                      00493
         ORG   TRTABLE+C'J'                                             00494
         DC    C'JKLMNOPQR      \ '                                     00495
         ORG   TRTABLE+C'S'                                             00496
         DC    C'STUVWXYZ      '                                        00497
         ORG   TRTABLE+C'0'                                             00498
         DC    C'0123456789      '                                      00499
         ORG                                                            00500
         SPACE                                                          00501
TABLE    DS    3000CL255                 MAX 3000 LINES PER ENTRY       00502
         EJECT                                                          00503
NOKWTBL  DSECT                                                          00504
NOKW     DS    CL16364            ABOUT 800 KEYWORDS      CBT           00505
*OKW     DS    CL4096             ABOUT 800 KEYWORDS      CBT           00506
         EJECT                                                          00507
KWTBL    DSECT                                                          00508
KW       DS    CL49152                                                  00509
#PAN$AUD CSECT                                                          005094
$PAN#AUD DC    CL21'003CBT1342   06/14/82'                              005095
         END                                                            00510
